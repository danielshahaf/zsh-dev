#!/usr/bin/env zsh

# This is a helper script, invoked by zshdev-add-nnnnn-and-changelog through git
# to amend the current HEAD commit.
#
# The caller sets the environment variable $seqno.

## Declarations
readonly -i WIDTH=74 TAB_WIDTH=8
readonly seqno=$seqno
local title files summary
local logmsg
local entry

zmodload -F zsh/datetime b:strftime p:EPOCHSECONDS

## Validate our caller.
[[ $# -eq 0 && -n $seqno ]] || { echo >&2 "$0: Usage error"; exit 2 }

## Helper functions.
# Prepend the string $1 to the file $2.
prepend_to_file() {
  local prependum=$1
  local file=$2
  () { printf "0r %s\nw\nq\n" $1 | ed -s -- $file } =(<<<$prependum)
}

## Update ChangeLog.
rev=HEAD
title="$(strftime "%Y-%m-%d" $EPOCHSECONDS)  $(git log --no-walk --pretty="%aN  <%aE>" $rev --)"
files=( ${(0)"$(git diff -z --name-only "${rev}^..$rev" --)"} )
if (( $#files <= 0 )); then
  echo >&2 "$0: assertion failed: (\$#files > 0); \$#files is '$#files'"
  exit 2
fi
summary=`git log --no-walk --pretty=%s $rev --`
if (( $#files == 1 )) &&
   { [[ $files[1] == ${summary%%: *} ]] || [[ ${files[1]##*/} == ${summary%%: *} ]] }; then
  # Special case: if the commit touches one file, and the log message names that file
  # explicitly or by basename, don't repeat the file name twice in ChangeLog.
  summary=${summary#*: }
fi
entry=''
entry+=$title
entry+=$'\n\n'
entry+=$(print -r - "* $seqno: ${(j:, :)files}: $summary" | fmt -w $((WIDTH - TAB_WIDTH)) | sed $'s/^/\t/g')
entry+=$'\n\n'
# Don't duplicate $title
if [[ "$title" == "$(head -n1 < ChangeLog)" ]]; then
  print -l '1,2d' 'w' 'q' | ed -s -- ChangeLog
fi
prepend_to_file $entry ChangeLog

## Commit ChangeLog, amend the commit message.
logmsg="$seqno: `git log --no-walk --pretty=%B HEAD`"
git commit --amend -m "$logmsg" ChangeLog
